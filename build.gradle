plugins {
    id "java"
    id "maven-publish"
}

version = "0.1.0"
group = "com.terraforged"

configurations {
    shade
    compile.extendsFrom(shade)
}

repositories {
    mavenCentral()
    maven { url "https://io.terraforged.com/repository/maven/" }
}

dependencies {
    shade "me.dags:Noise2D:0.5.0"
    compileOnly "it.unimi.dsi:fastutil:8.2.1"
}

jar {
    from { configurations.shade.collect { it.isDirectory() ? it : zipTree(it) } }
    duplicatesStrategy = "EXCLUDE"
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact jar
        }
    }

    if (System.getenv("MAVEN_USER") != null && System.getenv("MAVEN_PASS") != null) {
        repositories {
            maven {
                name = "nexus"
                url = "https://io.terraforged.com/repository/maven/"
                credentials {
                    username System.getenv("MAVEN_USER")
                    password System.getenv("MAVEN_PASS")
                }
            }
        }
    }
}